Domain meatSaleDomain
  Buyer isA Role with warehouse: String;
  Seller isA Role with returnAddress: String, name: String;
  Currency isAn Enumeration(CAD, USD, EUR);
  MeatQuality isAn Enumeration(PRIME, AAA, AA, A);
  Meat isA PerishableGood;
  PerishableGood isAn Asset with quantity: Number, quality: MeatQuality;
  Delivered isAn Event with item: Meat, deliveryAddress: String, delDueDate: Date;
  Disclosed isAn Event;
  Paid isAn Event with amount: Number, currency: Currency, from: Buyer, to: Seller, payDueDate: Date;
  PaidLate isAn Event with amount: Number, currency: Currency, from: Buyer, to: Seller;

endDomain

Contract MeatSale( amt: Number, buyer: Buyer, curr: Currency, delAdd: String, delDueDateDays: Number, effDate: Date, interestRate: Number, payDueDate: Date, qlt: MeatQuality, qnt: Number, seller: Seller )

Declarations
  delivered: Delivered with item := goods, deliveryAddress := delAdd, delDueDate := Date.add(effDate, delDueDateDays, days);
  disclosed: Disclosed;
  goods: Meat with quantity := qnt, quality := qlt;
  paid: Paid with amount := amt, currency := curr, from := buyer, to := seller, payDueDate := payDueDate;
  paidLate: PaidLate with amount := (1 + interestRate / 100) * amt, currency := curr, from := buyer, to := seller;

Preconditions
  IsOwner(goods, seller);

Postconditions
  IsOwner(goods, buyer) and not IsOwner(goods, seller);

Obligations
  delivery: Obligation(seller, buyer, true, WhappensBefore(delivered, delivered.delDueDate));
  latePayment: Happens(Violated(obligations.payment)) -> Obligation(buyer, seller, true, Happens(paidLate));
  payment: Obligation(buyer, seller, true, WhappensBefore(paid, paid.payDueDate));

Surviving Obligations
  so1: Obligation(seller, buyer, true, not WhappensBefore(disclosed, Date.Add(Activated(self), 6, months)));
  so2: Obligation(buyer, seller, true, not WhappensBefore(disclosed, Date.Add(Activated(self), 6, months)));

Powers
  resumeDelivery: HappensWithin(paidLate, Suspension(obligations.delivery)) -> Power(buyer, seller, true, Resumed(obligations.delivery));
  suspendDelivery: Happens(Violated(obligations.payment)) -> Power(seller, buyer, true, Suspended(obligations.delivery));
  terminateContract: Happens(Violated(obligations.delivery)) -> Power(buyer, seller, true, Terminated(self));

Constraints
  not IsEqual(buyer, seller);
  CannotBeAssigned(suspendDelivery);
  CannotBeAssigned(resumeDelivery);
  CannotBeAssigned(terminateContract);
  CannotBeAssigned(delivery);
  CannotBeAssigned(payment);
  CannotBeAssigned(latePayment);
  delivered.delDueDate < paid.payDueDate;

endContract