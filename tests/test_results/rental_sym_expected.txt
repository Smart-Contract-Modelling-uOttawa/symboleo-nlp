Domain propertyRentalDomain
  Landlord isA Role with name: String;
  Renter isA Role with name: String;
  Currency isAn Enumeration(CAD, USD, EUR);
  PaymentMethod isAn Enumeration(Cash, Cheque, ETransfer);
  RentalProperty isAn Asset with address: String;
  Paid isAn Event with amount: Number, currency: Currency, paymentMethod: PaymentMethod, from: Role, to: Role;
  Occupy isAn Event with agent: Role, property: RentalProperty;
  ProvideTerminationNotice isAn Event with agent: Role, daysInAdvance: Number;
  Abandon isAn Event with agent: Role, property: RentalProperty;
  KeepPets isAn Event with agent: Role;
  AllowPets isAn Event with grantor: Role;
  DatePasses isAn Event with date: Date;

endDomain

Contract PropertyRental( landlord: Landlord, renter: Renter, the_address: String, the_currency: Currency, the_rent_amount: Number, the_payment_method: PaymentMethod, the_late_fine: Number, the_deposit_amount: Number, var_daysInAdvance: Number, X: Date )

Declarations
  evt_date_passes: DatePasses with date := X;
  evt_pay_rent: Paid with amount := the_rent_amount, currency := the_currency, paymentMethod := the_payment_method, from := renter, to := landlord;
  evt_pay_late_fine: Paid with amount := the_late_fine, currency := the_currency, paymentMethod := the_payment_method, from := renter, to := landlord;
  evt_pay_deposit: Paid with amount := the_deposit_amount, currency := the_currency, paymentMethod := the_payment_method, from := renter, to := landlord;
  evt_return_deposit: Paid with amount := the_deposit_amount, currency := the_currency, paymentMethod := the_payment_method, from := landlord, to := renter;
  evt_occupy: Occupy with agent := renter, property := the_property;
  evt_provide_termination_notice: ProvideTerminationNotice with agent := landlord, daysInAdvance := var_daysInAdvance;
  evt_abandon: Abandon with agent := renter, property := the_property;
  evt_keep_pets: KeepPets with agent := renter;
  evt_allow_pets: AllowPets with grantor := landlord;
  the_property: RentalProperty with address := the_address;

Preconditions

Postconditions

Obligations
  ob_pay_rent: Happens(evt_date_passes) -> Obligation(renter, landlord, true, Happens(evt_pay_rent));
  ob_late_payment: Happens(Violated(obligations.ob_pay_rent)) -> Obligation(renter, landlord, true, Happens(evt_pay_late_fine));
  ob_pay_security_deposit: Obligation(renter, landlord, true, WhappensBeforeE(evt_pay_deposit, evt_occupy));
  ob_return_deposit: Happens(Terminated(self)) -> Obligation(landlord, renter, true, Happens(evt_return_deposit));
  ob_no_pets: Obligation(renter, landlord, true, not Happens(evt_keep_pets));

Surviving Obligations

Powers
  pow_termination_written: Happens(evt_provide_termination_notice) -> Power(renter, landlord, true, Terminated(self));
  pow_suspend_ob_no_pets: Happens(evt_allow_pets) -> Power(landlord, renter, true, Suspended(obligations.ob_no_pets));
  pow_termination_abandon: Happens(evt_abandon) -> Power(landlord, renter, true, Terminated(self));

Constraints

endContract