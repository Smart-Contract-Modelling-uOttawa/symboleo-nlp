Domain meatSaleDomain
  Seller isA Role with returnAddress: String, name: String;
  Buyer isA Role with warehouse: String;
  Currency isAn Enumeration(CAD, USD, EUR);
  MeatQuality isAn Enumeration(PRIME, AAA, AA, A);
  PerishableGood isAn Asset with quantity: Number, quality: MeatQuality;
  Meat isA PerishableGood;
  Delivered isAn Event with item: Meat;
  Paid isAn Event with amount: Number, currency: Currency, from: Buyer, to: Seller;
  PaidLate isAn Event with amount: Number, currency: Currency, from: Buyer, to: Seller;
  Disclosed isAn Event;

endDomain

Contract MeatSale( buyer: Buyer, seller: Seller, qnt: Number, qlt: MeatQuality, amt: Number, curr: Currency, payDueDate: Date, delAdd: String, effDate: Date, delDueDateDays: Number, interestRate: Number )

Declarations
  seller: Seller;
  buyer: Buyer;
  goods: Meat with quantity := qnt, quality := qlt;
  evt_delivered: Delivered with item := goods;
  evt_paid_late: PaidLate with amount := (1 + interestRate / 100) * amt, currency := curr, from := buyer, to := seller;
  evt_paid: Paid with amount := amt, currency := curr, from := buyer, to := seller;
  evt_disclosed: Disclosed;

Preconditions
  IsOwner(goods, seller);

Postconditions
  IsOwner(goods, buyer) and not IsOwner(goods, seller);

Obligations
  ob_delivery: Obligation(seller, buyer, true, WhappensBefore(evt_delivered, March 18, 2024));
  ob_payment: Obligation(buyer, seller, true, WhappensBefore(evt_paid, March 30, 2024));
  ob_late_payment: Happens(Violated(obligations.ob_payment)) -> Obligation(buyer, seller, true, Happens(evt_paid_late));

Surviving Obligations
  so1: SO(seller, buyer, true, not WhappensBefore(evt_disclosed, Date.Add(Terminated(self), 6, months)));
  so2: SO(buyer, seller, true, not WhappensBefore(evt_disclosed, Date.Add(Terminated(self), 6, months)));

Powers
  pow_suspend_delivery: Happens(Violated(obligations.ob_payment)) -> Power(seller, buyer, true, Suspended(obligations.ob_delivery));
  pow_resume_ob_delivery: Happens(Fulfilled(obligations.ob_late_payment)) -> Power(buyer, seller, true, Resumed(obligations.ob_delivery));
  pow_terminate_contract: Happens(Violated(obligations.ob_delivery)) -> Power(buyer, seller, true, Terminated(self));

Constraints
  not IsEqual(buyer, seller);
  CannotBeAssigned(suspendDelivery);
  CannotBeAssigned(resumeDelivery);
  CannotBeAssigned(terminateContract);
  CannotBeAssigned(delivery);
  CannotBeAssigned(payment);
  CannotBeAssigned(latePayment);
  delivered.delDueDate < paid.payDueDate;

endContract